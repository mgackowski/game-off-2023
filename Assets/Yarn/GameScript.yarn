title: InitialiseVariables
---
/// When starting a new game, start at this node to ensure the game state is reset.
<<declare $graphicsQuality = "Full" as string>>
<<maxZoom Scanner 1>>
<<displayTime ETA false>>
<<enhanceEnabled Scanner true>>

<<declare $nightclubFoundClues = 0 as number>>
<<declare $hospitalFoundSuspects = 0 as number>>
<<declare $officeCluesFound = 0 as number>>
<<declare $pinboardCluesFound = 0 as number>>

/// Files:
<<lockFile MissionBriefFile false>>
<<lockFile StreetViewFile true>>
<<lockFile NightclubFile true>>
<<lockFile HospitalFile true>>
<<lockFile PinboardFile true>>

// Animation and animation states
<<playAnimation ComputerUI Off>>

/// Hide any 3d elements and leave just the computer
<<setActive SceneComposer "3D Scene" false>>
<<setActive SceneComposer "Key" true>>
<<setActive SceneComposer "Office CM" false>>
<<setActive SceneComposer "Desk CM" false>>
<<setActive SceneComposer "Intro CM" false>>
<<setActive SceneComposer "Options List View" false>>
<<setActive SceneComposer "Menu Options List View" true>>

<<jump MainMenu>>
===

title: Intro
---
<<setActive SceneComposer "Options List View" true>>
<<setActive SceneComposer "Menu Options List View" false>>
<<disableMenu "Dialogue System">>
<<setActive SceneComposer "Intro CM" true>>   /// The intro camera activates and starts playing its animation
<<fadeOut BlackScreen 0>>
<<fadeIn BlackScreen 0.9>>
//<<wait  0.9>>

<<playMusic>>
<<setIntensity 0>>

Narrator:  01.12.1992<br>User logging on.///Inserting <br> Can be used to force a line break.
Narrator: Economic recessions have led to low morale, corruption and dissension. The world sees political divide, riots and extremism.
Narrator: New technology can combat the growing threat of disruption and violence.
Narrator: Groups have been planning a sinister attack to harm society - corporations, nuclear power plants, airports, even schools.
Narrator: You don't know their plan, but it's up to you to find and stop them.

<<playAnimation ComputerUI BootupStart>>

Narrator: You joined an organistation, combating organised crime. Armed with super-enhancing technology amateur hacking skills and a cool assistant.

<<playAnimation ComputerUI BootupFinish>>
<<wait 1>>
<<setActive SceneComposer "Intro CM" false>>  /// The view returns to the highest priority camera
<<wait 1>>

Assistant: Login confirmed. Let us not waste any time. Shall we?
    -> Shall we what?
    -> What's going on?
    -> But I'm just a squirrel!
Assistant: Not to worry. First, I recommend you get familiar with the mission brief.
Assistant: Look, that's it here - right in front of you.
Assistant: When you've had a proper look, let me know by giving it a <b>scan</b>.
    -> A scan?
        Assistant: Scanning is how you examine things in this world. Go ahead, have a read and then hit the scan button to let me know.
    -> I can handle this.
        Assistant: I'll be right around the corner.
===

title: MissionBrief
---
<<setIntensity 0.2>>
Assistant: There you go. That was a scan. You'll be doing a lot of that.
Player: It says here you've got more details for me.
Assistant: Ah, yes. Your task is to track down a group of particularily dangerous squirrels. Rioters. Terrorists. Real ruffians.
 -> I'm ready.
 -> Let's go!
        Assistant: Indeed!
 -> Who cares.
        Assistant: That's the spirit!
Assistant: We've got CCTV footage of possible suspects.

<<lockFile StreetViewFile false>>

Assistant: You should now be able to switch to that file. Give it a go!
===

title: StreetViewFile
---
/// This node plays when the file is first viewed

Assistant: Look at the picture. Try and work out if you see anything suspicious.
Player: What would count as suspicious?

Assistant: You could analyse the faces of the crowd. Inspect facial expressions closely. Look for any signs of aggression or peculiar expressions that might hint at their involvement in the riot. 

Assistant: Keep an eye out for unusual objects. Hidden signs or symbols. They could be discreet markings associated with criminal groups. If you find anything that stands out, scan it!

/// Trimming the dialogue down a bit to get players scanning faster
///Assistant: Look for backpacks, banners, or suspicious items that could lead to finding the people responsible for an attack!

Player: Okay, okay. Let's start.
Player: What is your name by the way?
Assistant: Why, I am Dr A. Corn!
Player: Really?

Dr A. Corn: Yes, why?
    -> No reason.
    -> Nut-thing.
    -> (Another terrible pun)

Dr A. Corn: In all honesty... I'm not sure why I need human assistance, but are you ready?
    ->Yes!
        Dr A. Corn: Splendid.
    ->I want to speak to your boss!
        Dr A. Corn: Sure, Karen. 

<<maxZoom Scanner 5>>
<<displayTime ETA true>> // displays ETA in the corner
<<setTime ETA 3 weeks 1>> // value: 3, unit: weeks, this will fluctuate by 1 in either direction
<<setIntensity 0.4>>
===

title: StreetView
---
Dr A. Corn: The scene shows a street view from a cctv camera.
Player: Any thoughts?
Dr A. Corn: We are tracking the movements of specific individuals. At least two squirrels.
Dr A. Corn: Examine the group dynamics within the image. Identify clusters of people working together or communicating non-verbally. This could provide insights into coordinated efforts during the protest.
Dr A. Corn: You won't be able to see much from here - you should zoom in!

///Dr A. Corn: Look for clothing or accessories associated with activist groups. Additionally, pay attention to any symbols or markings that may indicate affiliation with a particular cause or movement. 

///Dr A. Corn: For now, review the background details. Check for graffiti, posters, or other elements that might link the suspects to a specific protest or event. These details can help establish a timeline of activities. Report back with your findings.
===

title: Bench
---
Player: A couple sits on a bench, but they appear to be angry - could their argument have something to do with the case?
Player: They exude an air of suspicion. I think they're holding something.
Dr A. Corn: Ah, perfect timing.
Dr A. Corn: I've been running an analytical subroutine using a sophisticated neural network to reconstruct images.
Dr A. Corn: See that ETA counter in the corner? Once I'm done, this case is as good as closed.
Player: What counter? The one that says 2... no, 3 weeks?
Dr A. Corn: Why do you think you're here, sport? The more input we feed into the algorithm, the better it will get.
Dr A. Corn: In fact, I've already done enough work to recostruct the details of this scene!
Dr A. Corn: Go on, press the <b>enhance</b> button.
===

title: BenchE5x
---
Player: Whoa.
Dr A. Corn: Hm... we have flowers on the floor, a game in their hand and the likely potential that a couple have had an argument.
Player: Probably unimportant. Although it's a shame we caught them at a private moment.
Dr A. Corn: We now have the power to increase your zoom to <b>20x</b>. Use it wisely!
<<maxZoom Scanner 20>>
<<setTime ETA 25 days 3>> // value: 25, unit: days, this will fluctuate by 3 in either direction
===

title: SuspiciousPerson
---
Dr A. Corn: Upon closer inspection, you see a suspicious person, they are darting around and stand in contrast to the rest of their surroundings.
Dr A. Corn: However, they do not appear related to our case.
===

title: FigureInWindow
---
Player: Why does no-one shut their curtains these days?
===

title: TireTracks
---
Player: Driving in a pedestrian zone? Not cool.
===

title: PhoneBox
---
Player: I still have my Blue Box somewhere.
Dr A. Corn: Heavens, I dearly hope you're not one of those hoodlums who hack into phone boxes?
Player: What? No, I have a model police box. You know, the one that's bigger on the inside?
Dr A. Corn: "... Ex-PLAIN!
Player: "Maybe some other time.
===

title: Tamagotchi
---
Player: Hey, these things are all the rage right now.
===

title: Cafe
---
Player: I used to eat there. But they're cashew only.
===

title: OtherFlyers
---
Player: An old poster for the famous musical, "West Side Squeaker".
===

title: Flowers
---
Player: What a waste of flowers... they look so tasty, too.
===

title: ID
---
Player: Someone's not buying any drinks tonight!
===

title: Graffiti
---
Dr A. Corn: Some graffiti on the walls.
===

title: GraffitiE15
---
Dr A. Corn: The wall is adorned with surreal fluorescent moths, not the usual signature or simple picture that we might expect to see. The moths could be a code, and a clue. 
<<if visited("FlyerE15x")>>
    Player: The flyer! They're the same moths.
    Dr A. Corn: A keen observation.
    <<jump StreetSceneSolved>>
<<endif>>
===

title: Flyer
---
Player: I can't make this out.
===

title: FlyerE15x
---
Player: I get the acorn... but moths?
Dr A. Corn: The moths are insignia of a nightclub and local music scene. Many of the cities troublesome squirrels lurk in it.
<<if not visited("GraffitiE15")>>
    Player: Hey, haven't we seen something like this just a second ago?
<<else>>
    Player: It's just like the graffiti on the wall here.
    <<jump StreetSceneSolved>>
<<endif>>
===

title: CCTV
---
Player: Lenses everywhere, capturing the moves of the squirrels on the streets. Strategically placed around the cityscape.
Player: All the times I've walked past that cafe, sat and saw my friends...
Player: But the cameras make us safer. Their watchful eye is comforting.
Dr A. Corn: It's thanks to those cameras we can keep people safe, together. 
===

title : StreetSceneSolved
---
<<setTime ETA 10 days 3>>
Player: This may be a long shot, but...
Player: Dr Corn, can you interface with the surveillance network and pull an image from the nightclub?
Dr A. Corn: Done.
<<lockFile NightclubFile false>>
Player: (I can now switch to a new file.)
===

title: NightclubFile
---
Player: Looks like the middle of a rave!
Dr A. Corn: Let's investigate.
===

title: NightclubView
---
Player: It's a rave. Many of the faces seem to be obscured by pastel hues of holographic masks and fragmented, shifting, chaotic expressions, creating an uncanny vibe.
Dr A. Corn: Identification is impossible.
===

title: Dance
---
Player: Care for a dance, Dr A?
Dr A. Corn: I am not programmed to dance.
Player: All right, all right.
Player: ...Square.
===

title: Scar
---
Player: A scar, across her left eye... what happened to her?
===

title: MothPin
---
Dr A. Corn: This accessory's shape bears a 98% similarity to the moth insignia we've seen earlier.
Player: ...
===

title: BartenderGlasses
---



Player: You can see her ID the reflection!
<<stopMusic>>
Player: Wait a second...!
Player: !
Player: She's...
Player: No, can it really be her?
Dr A. Corn: Is something the matter?
Player: ...
<<playMusic>>
Player: (I didn't recognise her with the scar, but now...)
Player: Let's continue the investigation.

<<if not visited("BartenderGlasses")>>
    <<set $nightclubFoundClues = $nightclubFoundClues + 1>>
<<endif>>

<<if $nightclubFoundClues is 3>>
    <<jump NightclubSolved>>
<<endif>>
===

title: BartenderKey
---
Player: What's this?
Dr A. Corn: It appears to be a key. I can cross-reference its shape with my database of known locks in the city.
Player: OK, go ahead.
Dr A. Corn: There is one exact match...
Dr A. Corn: ...
Player: What's wrong?
Dr A. Corn: My apologies, I think I simply forgot what it was.
Player: Forgot? How can you forget?
Dr A. Corn: There are no matches for this query.

<<if not visited("BartenderKey")>>
    <<set $nightclubFoundClues = $nightclubFoundClues + 1>>
<<endif>>

<<if $nightclubFoundClues is 3>>
    <<jump NightclubSolved>>
<<endif>>
===

title: ScarLady
---
<<if visited("BartenderGlasses")>>
    Player: It's... it's really her.
    Dr A. Corn: It is very probable that this individual is here to conduct criminal activities.
    Dr A. Corn: Do you know anything about their identity?
    Player: ...
<<else>>
    Player: She looks... familiar.
<<endif>>
===

title: Arcade
---
Player: Such intense concentration... maybe they're about to beat the high score?
===

title: AIGlasses
---
Player: These glasses... they're the kind that can confuse cameras and AI systems, right?
Dr A. Corn: It appears that those are your typical kit for the anti-AI protesters.
Player: This clubs seems full of them.
===

title: Mask
---
Player: We've been seing those anti-AI masks more often.
===

title: BusTicket
---
Player: What is this slip of paper?
Dr A. Corn: It appears to be a bus ticket. Allow me to cross-reference it with the city's transit network...
Dr A. Corn: This ticket was used to travel on line 380 from Central Station A to St Mary's General Hospital on the 10th of November, or three weeks ago.
Dr A. Corn: The journey time was 23 minutes, five minutes more than scheduled. The ticket was purchased with cash.
Player: Ok, that's a lot of information. So, this guy went to a hospital - three weeks ago?
Player: How old is this footage?
Dr A. Corn: The footage you are looking at was taken approximately... 22 years and 11 months ago.
Player: What?!
Dr A. Corn: My apologies. It looks like the feed from this camera is being tampered with.
Dr A. Corn: Unless it's 01/01/1970 today, we must assume that the timestamp on this footage is invalid.
Player: OK. But this guy went to the hospital three weeks ago, right?
Dr A. Corn: That is the date that matches the ticket.

<<if not visited("BusTicket")>>
    <<set $nightclubFoundClues = $nightclubFoundClues + 1>>
<<endif>>

<<if $nightclubFoundClues is 3>>
    <<jump NightclubSolved>>
<<endif>>
===

title: Rave
---
Player: It's a squirrave!
Dr A. Corn: Please focus.
===

title: Shirt
---
Player: Interesting shirt choice.
===

title: BrokenCamera
---
Player: Hmm... it appears to be broken.
===

title: Umbrella
---
Player: The little umbrella makes the whole drink taste better, for sure.
===

title: Bar
---
Player: Some interesting exchange at the bar?
Dr A. Corn: We can't really tell at this level of detail.
===

title: EmptyGlasses
---
Player: All the glasses are empty.
Dr A. Corn: Clearly, this discussion has been going on for a while.
===

title: WrongOrder
---
Player: Perhaps they didn't get the right order.
===

title: BarE
---
Player: Well well, what do we have here...
Player: Looks like she's being checked for ID.
Dr A. Corn: I suggest we examine this scene in more detail.
===

title: Table
---
Player: They seem to stand out, even in a place like this. Maybe we could learn more here?
===

title: TableE
---
Player: We seem to have stumbled upon an interesting crowd.
Dr A. Corn: The headgear employs an anti-facial recognition system. The masks here are used to interfere with computer vision.
Player: Meaning?
Dr A. Corn: Meaning, they can avoid being recognised through the city's cameras.
Player: So we should definitely pay attention to these guys. Looks like we're in the right place.
===

title: NightclubSolved
---
Player: OK, let's summarise.
Dr A. Corn: The patrons of this club appear to be affiliated with various anti-AI movements.
Dr A. Corn: They are composed of climate groups, alt right groups, and otherwise unaffiliated criminals.
Player: And the hospital link? Are patients in danger?
<<setTime ETA 20 hours 3>>
Dr A. Corn: We cannot exclude the possibility. I have downloaded footage from the hospital, based on the arrival time we got from the bus ticket.
Dr A. Corn: Please examine it closely.
<<lockFile HospitalFile false>>
Player: (OK. Time to switch files again.)
===

title: HospitalFile
---
<<setIntensity 0.6>>
Player: What the... Dr Corn, what kind of feed is this?
Dr A. Corn: One of the hospital staff must have left their camera-enabled personal device idle at reception.
Player: We can access that?!
Dr A. Corn: Of course we can. It's 1992. Everything is network-enabled.
Dr A. Corn: Now, let us investigate.
===

title: Reception
---
Player: Another one of those masks. Those anti-AI guys are here, all right. But what are they doing at the hospital?

<<if not visited("Reception")>>
    <<set $hospitalFoundSuspects = $hospitalFoundSuspects + 1>>
<<endif>>
<<if $hospitalFoundSuspects is 3>>
    <<jump HospitalSolved>>
<<endif>>
===

title: Toys
---
Player: "Let me out, I can't bear it in this box anymore!"
===

title: Protester
---
Player: I recognise the jacket.
Dr A. Corn: Definitely a connection to the rioters.
Player: They look like they're in bad shape.
<<if not visited("Protester")>>
    <<set $hospitalFoundSuspects = $hospitalFoundSuspects + 1>>
<<endif>>
<<if $hospitalFoundSuspects is 3>>
    <<jump HospitalSolved>>
<<endif>>
===

title: Sign
---
Player: An anti-AI sign.
===

title: Newspaper
---
Player: Are they angry or do they need glasses?
===

title: Flu
---
Player: Whatever it is, you shouldn't sneeze on people.
===

title: TV
---
Player: A TV for everyone to watch while they're waiting. But who watches the watchers?
Player: We do.
===

title: LabCoat
---
Player: You can tell they're a professional. They have the labcoat.
===

title: Slogans
---
Player: Banners with slogans on them.
Dr A. Corn: "A network of lies", "Unplug the Network", "Plants Seeds for Privacy".
Player: So those eco activists are here at the hospital as well...
<<if not visited("Slogans")>>
    <<set $hospitalFoundSuspects = $hospitalFoundSuspects + 1>>
<<endif>>
<<if $hospitalFoundSuspects is 3>>
    <<jump HospitalSolved>>
<<endif>>
===

title: Ward
---
Player: Who designed this hospital? Why is the X-ray room so far from the ward?
===

title: XRayRoom
---
Player: I can't see through. The window is completely opaque.
===

title: OtherMedicalHistory
---
Player: No. I'm not checking some random person's medical history.
===

title: HospitalSolved
---
<<setTime ETA 50 minutes 9>>
Player: They're all connected to the anti-AI movement.
Player: Corn, can we see other rooms?
<<maxZoom Scanner 100>>
Dr A. Corn: The zoom cabalility is now set to 100x.
Dr A. Corn: However, this is the only angle we can observe from.
Dr A. Corn: Where can we find a new view on this case? 
Player: I guess we'll have to see where this takes us.
===

title: Door
---
Player: There's a window. You can see into the ward.
===

title: DoorE
---
Dr A. Corn: Interesting. Let's try a 1000x zoom.
<<maxZoom Scanner 1000>>
Player: Just how much can you keep enhancing those images? This is getting ridiculous.
<<setTime ETA 3 minutes 1>>
Dr A. Corn: My subroutine is almost complete. We're close.
<<lockHotspot BrokenMaskHotspot true>>
<<lockHotspot PhotoHotspot true>>
===

title: ScarLadyHospital
---
<<stopMusic>>
Player: It's...!
Dr A. Corn: This is the woman from the bar.
Player: What's wrong with her?! Where is she now?!
Dr A. Corn: It's a hospital. They're bound to have some kind of record.
<<playMusic>>
===

title: MedicalHistory
---
<<if not visited("ScarLadyHospital")>>
    Player: No. I'm not checking some random person's medical history.
<<else>>
    Dr A. Corn: Date: 10th November, 1992.
    Dr A. Corn: Injury: multiple lacerations and heavy scarring across left eye, when blunt force impact to head caused electronic face mask to shatter.
    Player: Three weeks ago... right after that big protest...
    Player: The protest who's participants I am supposed to hunt down.
    <<lockHotspot PhotoHotspot false>>
    <<lockHotspot BrokenMaskHotspot false>>
    Dr A. Corn: There is more here to investigate. Let us check her possessions for possible accomplices.
    Dr A. Corn: My analysis is almost complete. We have enough power for a 2000x zoom.
    <<maxZoom Scanner 2000>>
<<endif>>
===

title: BrokenMask
---
Player: She must have gotten hit in the face when protesting. And it left a nasty scar...
===

title: Photo
---
Player: A get well card, and... what's this? A photo?
<<stopMusic>>
Player: That's...
Player: That's me.
Dr A. Corn: What is the meaning of this? Why are you on this photograph?
Player: I didn't...
<<jump PhoneCall>>
===

title: PhoneCall
---
<<playSound 0 true>> // Phone ring
<<wait 1>>
Player: Someone's calling me?
<<wait 0.5>>
<<stopSound 0>>
<<playSound 1 false>> // Phone button click
<<wait  1.5>>

Familiar voice: Surprised to see you?
Player: Piney?! Piney, is that you?
Player: Piney. What's going on? Are you ok? What are you involved in?
Piney: It's good to hear your voice, dear brother.
Player: Piney. We couldn't find you for years. You completely fell off the grid.
Piney: I had a good reason.
Piney: And I could really use your help now.
Player: Of course. Anything.
Piney: But first, there's something I'd like you to see.
<<setTime ETA 0 seconds 0>>
<<playMusic>>
Dr A. Corn: SUBROUTINE COMPLETE.
Player: Doc? Doctor Corn?
Dr A. Corn: ZOOM CAPABILITY EXPANDED.
Piney: I know in my heart that you can make the right decision.
Piney: You just need to take a big step back.
<<wait  0.5>>
<<playSound 2 false>> // hang up sound
<<wait 1.5>>
Player: A big step back...
<<setIntensity 0.8>>
<<setActive SceneComposer "3D Scene" true>>
<<specialZoomout Scanner true>>
===

title: MaxZoomoutAchieved
---
<<setIntensity 1>>
Player: Holy...
Player: They're looking for me. I'm going to be a suspect.
Player: ... OK, this is no time to panic. I need to look around.
<<jump InvestigateOffice>>
===

title: InvestigateOffice
---
<<setActive SceneComposer "Office CM" true>>
<<if $officeCluesFound is 3>>
    <<jump OfficeSolved>>
<<endif>>

-> Check computer
    <<jump InvestigateOfficeComputer>>
-> Check desk
    <<jump InvestigateOfficeDesk>>
-> Other cubicles
    <<jump InvestigateOfficeCubicles>>
===

title: InvestigateOfficeComputer
---
Player: Looks like Dr A. Corn's programming was compromised somehow. Was all of this planned?
<<if not visited("InvestigateOfficeComputer")>>
    <<set $officeCluesFound = $officeCluesFound + 1>>
<<endif>>
<<jump InvestigateOffice>>
===

title: InvestigateOfficeDesk
---
<<setActive SceneComposer "Desk CM" true>>
Player: All my things are intact.
Player: Wait... the key to my office? I'll grab that. I need to check something.
<<setActive SceneComposer "Key" false>>
<<if not visited("InvestigateOfficeDesk")>>
    <<set $officeCluesFound = $officeCluesFound + 1>>
<<endif>>
<<setActive SceneComposer "Desk CM" false>>
<<jump InvestigateOffice>>
===

title: InvestigateOfficeCubicles
---
<<setActive SceneComposer "Office CM" false>>
Player: How much time before someone knocks on my door?
<<if not visited("InvestigateOfficeCubicles")>>
    <<set $officeCluesFound = $officeCluesFound + 1>>
<<endif>>
<<jump InvestigateOffice>>
===

title: OfficeSolved
---
Player: OK, I think it's coming together.
<<setIntensity 0.6>>

<<setActive SceneComposer "Office CM" false>>
<<switchToPinboard "3D Scene">>
<<wait 1>>

/// Hide files
<<lockFile MissionBriefFile true>>
<<lockFile StreetViewFile true>> 
<<lockFile NightclubFile true>>
<<lockFile HospitalFile true>>

/// Make pinboard only file available
<<lockFile PinboardFile false>>
<<selectFile FileSwitcher PinboardFile>>

/// Hide computer interface
<<playAnimation ComputerUI PinboardMode>>

<<wait 2>>
<<specialZoomout Scanner false>> /// Disables zooming out again
<<setActive SceneComposer "3D Scene" false>>
<<maxZoom Scanner 3>>
<<enhanceEnabled Scanner false>>

Player: Let's "scan" around this pinboard and see what we have.
===

title: PinboardTicket
---
Player: We know from the ticket that around thr 10th of November, the protesters were at the hospital.
===

title: PineyAtHospital
---
Player: My sister, Piney. For so long, she seemed to have disappeared off the face of the earth.
Player: Now, it seems like she was one of the protesters.
Player: She has my picture open on the bedside table. Was she shinking about me?
<<if not visited("PineyAtHospital")>>
    <<set $pinboardCluesFound = $pinboardCluesFound + 1>>
<<endif>>
<<if $pinboardCluesFound is 7>>
    <<jump PinboardSummary>>
<<endif>>
===

title: ProtestersAtHospital
---
Player: The protests went poorly for the activists, with many injured.
Player: I definitely have enough footage to report them all...
<<if not visited("ProtestersAtHospital")>>
    <<set $pinboardCluesFound = $pinboardCluesFound + 1>>
<<endif>>
<<if $pinboardCluesFound is 7>>
    <<jump PinboardSummary>>
<<endif>>
===

title: MCPhoto
---
Player: That's me. Just your average squirrel. Working security for the government.
Player: Probably not for long.
<<if not visited("MCPhoto")>>
    <<set $pinboardCluesFound = $pinboardCluesFound + 1>>
<<endif>>
<<if $pinboardCluesFound is 7>>
    <<jump PinboardSummary>>
<<endif>>
===

title: PineyAtBar
---
Player: The bar footage is from after the protesters got injured.
Player: Piney already has the scar.
<<if not visited("PineyAtBar")>>
    <<set $pinboardCluesFound = $pinboardCluesFound + 1>>
<<endif>>
<<if $pinboardCluesFound is 7>>
    <<jump PinboardSummary>>
<<endif>>
===

title: BartenderPhoto
---
Player: Dr Corn mentioned this guy was notorious.
Player: The key in his pocket matches the one to my office. I knew it.
<<if not visited("BartenderPhoto")>>
    <<set $pinboardCluesFound = $pinboardCluesFound + 1>>
<<endif>>
<<if $pinboardCluesFound is 7>>
    <<jump PinboardSummary>>
<<endif>>
===

title: Key
---
Player: Looks like I'm not the only one with access to the office.
===

title: DrACorn
---
Player: They planted something into him. Some subroutine.
Player: He wasn't just increasing my zoom.
Player: They wanted me to see outward, to wake up and reach for the truth.
<<if not visited("DrACorn")>>
    <<set $pinboardCluesFound = $pinboardCluesFound + 1>>
<<endif>>
<<if $pinboardCluesFound is 7>>
    <<jump PinboardSummary>>
<<endif>>
===

title: Organisation
---
Player: Look, from a certain angle, you can make everything look like an oppresive regime.
<<if not visited("Organisation")>>
    <<set $pinboardCluesFound = $pinboardCluesFound + 1>>
<<endif>>
<<if $pinboardCluesFound is 7>>
    <<jump PinboardSummary>>
<<endif>>
===

title: TodaysDate
---
Player: That's today.
===

title: PinboardSummary
---
<<setIntensity 0>>
<<playSound 0 true>>
<<wait  1>>
Player: !
Player: Somebody's calling me again.
<<wait  0.5>>
<<stopSound 0>>
<<playSound 1 false>> // click!
<<wait 1.5>>
Piney: Do you see now?
Piney: The people who run this city, they don't really care about us.
Piney: The use of AI in city monitoring, its impact on our privacy, the environment - we never wanted any of it.
Piney: But they never listen. We've exhausted all possibilities.
Piney: We need someone within the system. We need you, my brother.
-> You know I'm with you, no matter what.
    Player: You know I'm with you, no matter what.
    Piney: Thank you.
-> What you're doing is illegal.
    Player: Piney, this isn't worth the trouble. You're running from the law. You'll get seriously hurt.
    Piney: Trust me. If we give up, it's going to hurt us more.

<<jump BigChoice>>
===

title: BigChoice
---
<<setIntensity 0.4>>
Piney: Will you join us?
Player: Piney...
Piney: It's ok. I know it's a big decision, and you don't have to decide now.
Piney: Let me know. You know where to find me now.
Player: ...
Piney: Oh, and brother?
Player: Yes?
Piney: Don't wait too long.
<<playSound 2 false>>
<<wait 1>>
<<jump TheEnd>>
===

title: TheEnd
---
<<setIntensity 1>>
<<fadeOut BlackScreen 4>>
<<wait 4>>
<<setActive SceneComposer Scanner false>>
<<setActive SceneComposer Credits true>>
===

title: Placeholder
---
Player: We still need to write dialogue for this one.
===

title: EnhanceFailed
---
///Display one of a few messages randomly.
<<declare $enhanceFailedMessage = 0 as number>>
<<set $enhanceFailedMessage = dice(3)>>

<<if $enhanceFailedMessage == 1>>
    Dr A. Corn: There's nothing to really enhance here, I'm afraid.
<<elseif $enhanceFailedMessage == 2>>
    Dr A. Corn: Nothing here.
<<else>>
    Dr A. Corn: I am not detecting anything worth enhancing here.
<<endif>>
===